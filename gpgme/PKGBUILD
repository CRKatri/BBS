# Maintainer: Tobias Powalowski <tpowa@archlinux.org>
# Contributor: Roman Kyrylych <roman@archlinux.org>
# Contributor: Sarah Hay <sarah@archlinux.org>

pkgname=gpgme
pkgver=1.14.0
pkgrel=1
_python_ver=3.8
pkgdesc="A C wrapper library for GnuPG"
arch=('arm64')
url='https://www.gnupg.org/related_software/gpgme/'
license=('LGPL')
makedepends=('libgpg-error' 'gnupg' 'python' 'swig')
depends=('libgpg-error' 'gnupg>=2')
options=('!emptydirs')
provides=('libgpgme.dylib'
          'libgpgmepp.dylib')
source=("https://www.gnupg.org/ftp/gcrypt/${pkgname}/${pkgname}-${pkgver}.tar.bz2"{,.sig})
sha256sums=('cef1f710a6b0d28f5b44242713ad373702d1466dcbe512eb4e754d7f35cd4307'
            'SKIP')
validpgpkeys=('D8692123C4065DEA5E0F3AB5249B39D24F25E3B6') # Werner Koch

build() {
  cd ${pkgname}-${pkgver}

  ./configure \
    --prefix=/usr \
    --host=${GNU_HOST_TRIPLE} \
    --disable-fd-passing \
    --disable-static \
    --disable-gpgsm-test
  make
}

package() {
  cd ${pkgname}-${pkgver}

  make DESTDIR="${pkgdir}" install
  make DESTDIR="${BBS_BASE}" install

  find $pkgdir -type f -exec ldid -S${BBS_EXTRA}/general.xml {} \; &> /dev/null
  find $pkgdir -name '.ldid*' -type f -delete
}
