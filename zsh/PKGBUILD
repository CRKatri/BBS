# Maintainer: Pierre Schmitz <pierre@archlinux.de>

pkgname=zsh
pkgver=5.8
pkgrel=1
arch=('arm64')
url='https://www.zsh.org/'
license=('custom')
pkgdesc="A very advanced and programmable command interpreter (shell) for UNIX"
makedepends=('pcre' 'ncurses')
source=("https://www.zsh.org/pub/zsh-${pkgver}.tar.xz"{,.asc})
sha512sums=('96198ecef498b7d7945fecebbe6bf14065fa8c5d81a7662164579eba8206b79575812d292adea1864bc7487ac0818ba900e25f9ab3802449340de80417c2c533'
            'SKIP')
validpgpkeys=('F7B2754C7DE2830914661F0EA71D9A9D4BDB27B3'
              'E96646BE08C0AF0AA0F90788A5FEEE3AC7937444'
              '7CA7ECAAF06216B90F894146ACF8146CAE8CBBC4')

prepare() {
	cd "${srcdir}/${pkgbase}-${pkgver}"

  sed -i 's/|| eno == ENOENT)/|| eno == ENOENT || eno == EPERM)/' Src/exec.c
  sed -i 's/(eno == ENOEXEC)/(eno == ENOEXEC || eno == EPERM)/' Src/exec.c
}

build() {
	cd "${srcdir}/${pkgbase}-${pkgver}"

  ./configure \
    --host=${GNU_HOST_TRIPLE} \
    --prefix=/usr \
    --enable-cap \
    --enable-pcre \
    --enable-multibyte \
    --enable-zsh-secure-free \
    --enable-unicode9 \
    --with-tcsetpgrp \
    --enable-function-subdirs \
    --disable-gdbm \
    DL_EXT=bundle \
    zsh_cv_shared_environ=yes \
    zsh_cv_sys_elf=no \
    zsh_cv_sys_dynamic_execsyms=yes \
    zsh_cv_rlimit_rss_is_as=yes \
    zsh_cv_path_utmpx=/var/run/utmpx \
    zsh_cv_path_utmp=no \
    ac_cv_prog_PCRECONF="${BBS_ROOT}/pcre/pkg/pcre/usr/bin/pcre-config"

	make
}

package() {
  cd "${srcdir}/$pkgname-$pkgver"
  make install DESTDIR="$pkgdir"
  rm $pkgdir/usr/bin/zsh-$pkgver

  find $pkgdir -type f -exec ldid -S${BBS_EXTRA}/general.xml {} \; &> /dev/null
  find $pkgdir -name '.ldid*' -type f -delete
}
