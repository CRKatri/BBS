# Maintainer: Cameron Katri <katri.cameron@gmail.com>

pkgname=gnupg
pkgver=2.2.23
pkgrel=1
pkgdesc='Complete and free implementation of the OpenPGP standard'
url='https://www.gnupg.org/'
license=('GPL')
arch=('arm64')
depends=('npth' 'libgpg-error' 'libgcrypt' 'libksba' 'libassuan')
validpgpkeys=('D8692123C4065DEA5E0F3AB5249B39D24F25E3B6'
              '46CC730865BB5C78EBABADCF04376F3EE0856959'
              '031EC2536E580D8EA286A9F22071B08A33BD3F06'
              '5B80C5754298F0CB55D8ED6ABCEF7E294B092E28')
source=("https://gnupg.org/ftp/gcrypt/${pkgname}/${pkgname}-${pkgver}.tar.bz2"{,.sig})
sha256sums=('10b55e49d78b3e49f1edb58d7541ecbdad92ddaeeb885b6f486ed23d1cd1da5c'
            'SKIP')

build() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	./configure \
		--host=${GNU_HOST_TRIPLE} \
		--prefix=/usr \
		--sysconfdir=/etc \
		--sbindir=/usr/bin \
		--libexecdir=/usr/lib/gnupg \
		--disable-gnutls \
		--with-gpg-error-prefix=${BBS_BASE}/usr \
		--with-libassuan-prefix=${BBS_BASE}/usr \
		--with-npth-prefix=${BBS_BASE}/usr \
		--with-libgcrypt-prefix=${BBS_BASE}/usr \
		--with-ksba-prefix=${BBS_BASE}/usr \
		--with-bzip2
	
	make
}

package() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	make DESTDIR="${pkgdir}" install
	make DESTDIR="${BBS_BASE}" install

	find $pkgdir -type f -exec ldid -S${BBS_EXTRA}/general.xml {} \; &> /dev/null
  find $pkgdir -name '.ldid*' -type f -delete
}

# vim: ts=2 sw=2 noet:
