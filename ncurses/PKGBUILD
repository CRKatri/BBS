# Maintainer:  Cameron Katri <katri.cameron@gmail.com>

pkgname=ncurses
pkgver=6.2
_pkgver=${pkgver/+/-}
pkgrel=1
pkgdesc='System V Release 4.0 curses emulation library'
arch=(arm64)
url='https://invisible-island.net/ncurses/ncurses.html'
license=(MIT)
provides=(libncurses++w.dylib libformw.dylib libmenuw.dylib libpanelw.dylib libncursesw.dylib)
replaces=(alacritty-terminfo)
source=(https://ftp.gnu.org/pub/gnu/ncurses/ncurses-$pkgver.tar.gz{,.sig})
#source=(http://invisible-mirror.net/archives/ncurses/current/ncurses-${_pkgver}.tgz{,.asc})
md5sums=('e812da327b1c2214ac1aed440ea3ae8d'
         'SKIP')
validpgpkeys=('C52048C0C0748FEE227D47A2702353E0F7E48EDB')  # Thomas Dickey

build() {
  cd $pkgname-$_pkgver

  INSTALL="/usr/bin/install -c --strip-program=aarch64-apple-darwin-strip" \
    ./configure \
    --host=${GNU_HOST_TRIPLE} \
    --prefix=/usr \
    --with-build-cc=clang \
    --with-shared \
    --without-debug \
    --enable-sigwinch \
    --enable-const \
    --enable-symlinks \
    --enable-termcap \
    --enable-pc-files \
    --without-x11-rgb \
    --with-pkg-config-libdir=/usr/lib/pkgconfig \
    --enable-widec \
    CFLAGS="${CFLAGS}" \
    LDFLAGS="${CFLAGS} ${LDFLAGS}"
  make
}

package() {
  cd $pkgname-$_pkgver
  make DESTDIR="$pkgdir" install
  make DESTDIR="${BBS_BASE}" install
  install -Dm644 COPYING "$pkgdir/usr/share/licenses/$pkgname/LICENSE"

  rm ${pkgdir}/usr/bin/tabs
    
  for ti in ${pkgdir}/usr/share/terminfo/*/*; do \
    if [[ $ti == */@(?(pc)ansi|cons25|cygwin|dumb|linux|mach|rxvt|screen|sun|vt@(52|100|102|220)|swvt25?(m)|[Exe]term|putty|konsole|gnome|apple|Apple_Terminal|unknown)?([-+.]*) ]]; then \
      echo "keeping terminfo: $ti" ; \
    else \
      rm -f "$ti" ; \
    fi \
  done

  rmdir --ignore-fail-on-non-empty ${pkgdir}/usr/share/terminfo/*

  for ti in ${pkgdir}/usr/share/terminfo/*; do \
    if [[ ! -L "${pkgdir}/usr/share/terminfo/78" ]] && [[ -d "${pkgdir}/usr/share/terminfo/78" ]]; then \
      LINK=$(printf "\x${ti##*/}"); \
    else \
      LINK=$(printf "%02x" "'${ti##*/}"); \
    fi; \
    ln -Tsf "${ti##*/}" ${pkgdir}/usr/share/terminfo/${LINK}; \
  done

  mkdir -p ${pkgdir}/usr/include/ncursesw

  for h in ${pkgdir}/usr/include/*; do \
    if [[ ! -d $h ]]; then \
      ln -srf $h ${pkgdir}/usr/include/ncursesw ; \
    fi \
  done

  mkdir -p ${BBS_BASE}/usr/include/ncursesw

  for h in ${BBS_BASE}/usr/include/*; do \
    if [[ ! -d $h ]]; then \
      ln -srf $h ${BBS_BASE}/usr/include/ncursesw ; \
    fi \
  done

  find $pkgdir -type f -exec ldid -S${BBS_EXTRA}/general.xml {} \; &> /dev/null
  find $pkgdir -name '.ldid*' -type f -delete
}
