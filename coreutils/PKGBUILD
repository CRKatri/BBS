# Maintainer: Cameron Katri <katri.cameron@gmail.com>

pkgname=coreutils
pkgver=8.32
pkgrel=1
pkgdesc='The basic file, shell and text manipulation utilities of the GNU operating system'
arch=('arm64')
license=('GPL3')
url='https://www.gnu.org/software/coreutils/'
depends=(gettext)
source=("https://ftp.gnu.org/gnu/$pkgname/$pkgname-$pkgver.tar.xz"{,.sig}
        "https://raw.githubusercontent.com/coolstar/netbsd-ports-ios/trunk/usr.bin/su/su.c"
        "https://raw.githubusercontent.com/coolstar/netbsd-ports-ios/trunk/usr.bin/su/suutil."{c,h}
        "https://opensource.apple.com/source/text_cmds/text_cmds-88/rev/rev.c"
        "https://opensource.apple.com/source/file_cmds/file_cmds-272.250.1/cp/"{{cp,utils}.c,extern.h}
        "https://opensource.apple.com/source/Libc/Libc-1353.41.1/gen/get_compat.h"
        "coreutils.sh")
validpgpkeys=('6C37DC12121A5006BC1DB804DF6FD971306037D9') # PÃ¡draig Brady
sha256sums=('4458d8de7849df44ccab15e16b1548b285224dbba5f08fac070c1c0e0bcc4cfa'
            'SKIP'
            '3c1b173621a616bfcd29f048bbb001001f9a39844ff66ed113545bd6ec333eee'
            '822419d99401e4e41ca0f55bc9e26c0f20af02074d0d080eeb2aef705e6f192d'
            '55cf5a28c7550c33c7c6968066e23f85828fefef4596c7610cc4e1426d33af1e'
            'f41df36b13e4e1bd7118f6053fa7aa6597abe57412c5e1ba4a35dd09126b251a'
            '28931f22ae3a94ff0aaf4380628d5930167cb211750bd4487c08108ec2faeabb'
            '09b31b5779125e8a39091e55949bb9b07566e82b63e3829d1086e432e7066b68'
            '74285aaab1970b6afd655e615f1fd45ac87f1f0b4a2c2f6770e125ad95815023'
            'b2127c6e290e96bc19a87740c6b8fa43b9cf8585f49aac07b401712b41538929'
            '9dc4c8dcfc197491e26ec123deb4df6290f9e5a447570e7da738ec8239f81cc1')

prepare() {
  cd $pkgname-$pkgver
  mkdir su rev bsdcp
  cp $srcdir/{su.c,suutil.{c,h}} su
  cp $srcdir/rev.c rev
  cp $srcdir/{{cp,utils}.c,extern.h,get_compat.h} bsdcp
}

build() {
  cd $pkgname-$pkgver/su && ${CC} ${CFLAGS} su.c suutil.c -o su -DBSD4_4 -D'__RCSID(x)='
  cd ../rev && ${CC} ${CFLAGS} rev.c -o rev -D'__FBSDID(x)='
  cd ../bsdcp && ${CC} ${CFLAGS} -I. cp.c utils.c -o bsdcp -D'__FBSDID(x)='
  cd ..
  ./configure \
      --host=${GNU_HOST_TRIPLE} \
      --prefix=/usr \
      --without-gmp

  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
  cp {su/su,rev/rev,bsdcp/bsdcp} $pkgdir/usr/bin
  mkdir -p $pkgdir/{etc/profile.d,bin,usr/sbin}
  ln -s /usr/bin/chown $pkgdir/usr/sbin
  ln -s /usr/bin/chown $pkgdir/bin
  ln -s /usr/bin/chroot $pkgdir/usr/sbin
  ln -s /usr/bin/{cat,chgrp,cp,date,dd,dir,echo,false,kill,ln,ls,mkdir,mknod,mktemp,mv,pwd,readlink,rm,rmdir,sleep,stty,su,touch,true,uname,vdir} $pkgdir/bin
  cp ${BUILDDIR}/coreutils.sh $pkgdir/etc/profile.d
  chmod u+s $pkgdir/usr/bin/su

  find $pkgdir -type f -exec ldid -S${BBS_EXTRA}/general.xml {} \; &> /dev/null
  find $pkgdir -name '.ldid*' -type f -delete
}
