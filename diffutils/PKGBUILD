# Mainainer: SÃ©bastien Luttringer <seblu@archlinux.org>
# Contributor: Allan McRae <allan@archlinux.org>
# Contributor: Andreas Radke <andyrtr@archlinux.org>

pkgname=diffutils
pkgver=3.7
pkgrel=1
pkgdesc='Utility programs used for creating patch files'
arch=('arm64')
url='https://www.gnu.org/software/diffutils'
license=('GPL3')
depends=('bash' 'gettext')
validpgpkeys=('155D3FC500C834486D1EEA677FD9FCCB000BEEEE') # Jim Meyering
source=("https://ftp.gnu.org/gnu/$pkgname/$pkgname-$pkgver.tar.xz"{,.sig})
md5sums=('4824adc0e95dbbf11dfbdfaad6a1e461'
         'SKIP')

prepare() {
  cd $pkgname-$pkgver
  # apply patch from the source array (should be a pacman feature)
  local src
  for src in "${source[@]}"; do
    src="${src%%::*}"
    src="${src##*/}"
    [[ $src = *.patch ]] || continue
    msg2 "Applying patch $src..."
    patch -Np1 < "../$src"
  done
}

build() {
  cd $pkgname-$pkgver
  ./configure \
    --host=${GNU_HOST_TRIPLE} \
    --prefix=/usr \
    --disable-dependency-tracking

  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install

  find $pkgdir -type f -exec ldid -S${BBS_EXTRA}/general.xml {} \; &> /dev/null
  find $pkgdir -name '.ldid*' -type f -delete
}

# vim:set ts=2 sw=2 et:
