# Maintainer: Andreas Radke <andyrtr@archlinux.org>
# Contributor: judd <jvinet@zeroflux.org>

pkgname=libgpg-error
pkgver=1.39
pkgrel=1
pkgdesc="Support library for libgcrypt"
arch=(arm64)
url="https://www.gnupg.org"
license=('LGPL')
depends=('sh')
source=(https://www.gnupg.org/ftp/gcrypt/libgpg-error/${pkgname}-${pkgver}.tar.bz2{,.sig})
# https://www.gnupg.org/download/integrity_check.html
sha1sums=('991a7a53e778a3de5ba8aad711e0bc9453101f4b'
          'SKIP')
validpgpkeys=('D8692123C4065DEA5E0F3AB5249B39D24F25E3B6'  # Werner Koch
              '031EC2536E580D8EA286A9F22071B08A33BD3F06') # NIIBE Yutaka (GnuPG Release Key) <gniibe@fsij.org>

prepare() {
  cd ${pkgname}-${pkgver}
  sed -i '/{"armv7-unknown-linux-gnueabihf"  },/a \ \ \ \ {"${GNU_HOST_TRIPLE}",  "${GNU_HOST_TRIPLE}" },' src/mkheader.c
}

build() {
  cd ${pkgname}-${pkgver}
  ./configure \
    --prefix=/usr \
    --host=${GNU_HOST_TRIPLE} \
    --disable-nls

  make
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}/" install
  make DESTDIR="${BBS_BASE}/" install

  find $pkgdir -type f -exec ldid -S${BBS_EXTRA}/general.xml {} \; &> /dev/null    
  find $pkgdir -name '.ldid*' -type f -delete
}
