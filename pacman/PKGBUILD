# vim: set ts=2 sw=2 et:
# Maintainer: Dan McGee <dan@archlinux.org>
# Maintainer: Dave Reisner <dreisner@archlinux.org>

pkgname=pacman
pkgver=5.2.2
pkgrel=1
pkgdesc="A library-based package manager with dependency support"
arch=('arm64')
url="https://www.archlinux.org/pacman/"
license=('GPL')
depends=('bash' 'libarchive' 'curl' 'gpgme')
provides=('libalpm.dylib')
backup=(etc/pacman.conf
        etc/makepkg.conf)
options=('strip')
validpgpkeys=('6645B0A8C7005E78DB1D7864F99FFE0FEAE999BD'  # Allan McRae <allan@archlinux.org>
              'B8151B117037781095514CA7BBDFFC92306B1121') # Andrew Gregory (pacman) <andrew@archlinux.org>
source=(https://sources.archlinux.org/other/pacman/$pkgname-$pkgver.tar.gz{,.sig}
        ios.diff
        pacman.conf
        makepkg.conf)
sha256sums=('bb201a9f2fb53c28d011f661d50028efce6eef2c1d2a36728bdd0130189349a0'
            'SKIP'
            '0245a73097b4c07e967246d3c9da502e46df216f9029e3f43cefa5459f9550ab'
            'fafc24f6798d1d96eb0b658259b5b0e2769fedf9c3fba5030e2a1a4d563764cf'
            '6070e489c9fcf28b0a4e785351c5f376d1a22e24728466f6d476e5570cfa947a')

prepare() {
  cd "$pkgname-$pkgver"
  patch -p1 -i ../ios.diff
}

build() {
  cd "$pkgname-$pkgver"

  ./configure \
    --host=${GNU_HOST_TRIPLE} \
    --prefix=/usr \
    --sysconfdir=/etc \
    --disable-dependency-tracking \
    --disable-doc \
    --with-scriptlet-shell=/usr/bin/bash \
    --localstatedir=/private/var
  make V=1
}

package() {
  cd "$pkgname-$pkgver"

  make DESTDIR="$pkgdir" install
  make DESTDIR="${BBS_BASE}" install

  # install Arch specific stuff
  install -dm755 "$pkgdir/etc"
  install -m644 "$srcdir/pacman.conf" "$pkgdir/etc"
  install -m644 "$srcdir/makepkg.conf" "$pkgdir/etc"

  aarch64-apple-darwin-install_name_tool -change /usr/lib/libarchive.2.dylib /usr/lib/libarchive.13.dylib $pkgdir/usr/bin/pacman
  aarch64-apple-darwin-install_name_tool -change /usr/lib/libarchive.2.dylib /usr/lib/libarchive.13.dylib $pkgdir/usr/bin/pacman-conf
  aarch64-apple-darwin-install_name_tool -change /usr/lib/libarchive.2.dylib /usr/lib/libarchive.13.dylib $pkgdir/usr/bin/testpkg
  aarch64-apple-darwin-install_name_tool -change /usr/lib/libarchive.2.dylib /usr/lib/libarchive.13.dylib $pkgdir/usr/bin/vercmp
  aarch64-apple-darwin-install_name_tool -change /usr/lib/libarchive.2.dylib /usr/lib/libarchive.13.dylib $pkgdir/usr/lib/libalpm.12.dylib

  find $pkgdir -type f -exec ldid -S${BBS_EXTRA}/general.xml {} \; &> /dev/null    
  find $pkgdir -name '.ldid*' -type f -delete
}
